
apply from: file('gradle/min-gradle-version.gradle') 

ext {
  versions = [
    "randomizedtesting": "2.7.2",
    "junit": "4.12",
    "assertj": "3.11.1",
    "slf4j": "1.7.13",
    "morfologik": "2.1.5",
    "hppc": "0.8.1",

    "jetty": "9.4.15.v20190215",
    "log4j2": "2.11.2",
    "jackson": "2.9.8",
  ]

  legal = files('carrot2.LICENSE')
  replaceTokens = [
    "version": project.version
  ]
}


allprojects {
  apply plugin: 'idea'
  version = "4.0"

  repositories {
    mavenCentral()
  }

  idea {
    module {
      outputDir file('build/idea/classes/main')
      testOutputDir file('build/idea/classes/test')
      downloadSources = true
    }
  }

  afterEvaluate {
    if (project.plugins.hasPlugin(JavaPlugin)) {
      sourceCompatibility = 1.8
      targetCompatibility = 1.8

      compileJava.options.encoding = "UTF-8"
      compileTestJava.options.encoding = "UTF-8"

      dependencies {
        testImplementation "com.carrotsearch.randomizedtesting:randomizedtesting-runner:${versions.randomizedtesting}"
        testImplementation "junit:junit:${versions.junit}"
        testImplementation "org.assertj:assertj-core:${versions.assertj}"
      }

      tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
      }
    }
  }
}
