
import java.nio.file.Files
import org.gradle.util.GradleVersion

plugins {
  id 'net.ltgt.apt-idea' version "0.20" apply false
  id 'java-gradle-plugin'
}

String minimumGradleVersion = "5.1.1"
if (GradleVersion.current() < GradleVersion.version(minimumGradleVersion)) {
    throw new GradleException("Gradle ${minimumGradleVersion}+ is required (use local ./gradlew for wrapper).")
}

allprojects {
  version = '4.0'

  repositories {
    mavenCentral()
  }
}

configure(subprojects.findAll {[
  "carrot2-core",
  "carrot2-examples"
].contains(it.name)}) {
  apply plugin: 'java-library'
  apply plugin: 'net.ltgt.apt-idea'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  dependencies {
    testImplementation "com.carrotsearch.randomizedtesting:randomizedtesting-runner:2.7.2"
    testImplementation "junit:junit:4.12"
  }
}

configurations.all {
  transitive = false
}

project("carrot2-core") {
  dependencies {
    api "com.fasterxml.jackson.core:jackson-core:2.8.11"
    api "com.fasterxml.jackson.core:jackson-annotations:2.8.11"
    api "com.fasterxml.jackson.core:jackson-databind:2.8.11"
    api "org.carrot2.attributes:attributes-binder:1.3.3"
    api "org.slf4j:slf4j-api:1.7.13"

    implementation "org.carrot2.shaded:carrot2-guava:18.0"
    implementation("org.simpleframework:simple-xml:2.7.1") {
      exclude group: 'stax'
      exclude group: 'xpp3'
    }
    compile "org.apache.commons:commons-lang3:3.5"
    compile "com.carrotsearch:hppc:0.8.1"

    annotationProcessor "org.carrot2.attributes:attributes-processor:1.3.3"
    annotationProcessor "org.slf4j:slf4j-simple:1.7.13"
  }

  compileJava.dependsOn(processResources)
}

project("carrot2-examples") {
  dependencies {
    compile project(':carrot2-core')
  }
}
