
apply plugin: 'java-library'

if (file('lib').exists()) {
  dependencies {
    implementation fileTree(dir: 'lib', include: "**/*.jar")
  }
} else {
  dependencies {
    implementation project(':core')
    testImplementation project(':lang:polish')
    testImplementation project(':lang:lucene')
    testImplementation "org.apache.lucene:lucene-analyzers-icu:${versions.lucene}"
  }
}

configurations {
  distribution
}

task buildDistribution(type: Sync) {
  from(project.projectDir) {
    include 'build.gradle'
    include 'src/**'
    exclude 'src/main/assembly/**'
  }

  from(file("src/main/assembly")) {
    include '**/*'
  }

  from(configurations.runtimeClasspath + configurations.testRuntimeClasspath) {
    into 'lib'
  }

  into "$buildDir/distribution"
}

artifacts {
  distribution file("$buildDir/distribution"), {
    builtBy tasks.buildDistribution
  }
}
