
apply plugin: 'java'

archivesBaseName = 'carrot2-dcs-examples'

if (file('lib').exists()) {
  dependencies {
    implementation fileTree(dir: 'lib', include: "**/*.jar", exclude: "**/${archivesBaseName}*")
  }
} else {
  dependencies {
    implementation project(':core')
    implementation project(':examples')
    implementation project(':dcs:model')

    implementation "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    implementation "org.apache.httpcomponents:httpclient:${versions.httpclient}"
    implementation "com.carrotsearch.console:launcher:${versions.console}"
  }}

configurations {
  distribution
}

jar {
  doFirst {
    manifest {
      attributes("Main-Class": "com.carrotsearch.console.launcher.Launcher")
      attributes("Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '))
    }
  }
}

task buildDistribution(type: Sync) {
  from(project.projectDir) {
    include 'build.gradle'
    include 'src/**'
    exclude 'src/main/assembly/**'
  }

  from(file("src/main/assembly")) {
    include '**/*'
  }

  from(configurations.runtimeClasspath) {
    into 'lib'
  }

  from(jar) {
    into 'lib'
  }

  into "$buildDir/distribution"
}

artifacts {
  distribution file("$buildDir/distribution"), {
    builtBy tasks.buildDistribution
  }
}

