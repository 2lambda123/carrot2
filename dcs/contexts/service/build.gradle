
apply plugin: 'java'
apply plugin: 'war'

configurations {
  langResources
}

def aggregateProjects = [
  ":core",
  ":lang:polish",
  ":lang:lucene"
]

dependencies {
  implementation(project(':dcs:client')) {
    exclude group: 'org.slf4j'
  }
  implementation "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"

  aggregateProjects.each { prj ->
    implementation(project(prj)) {
      exclude group: 'org.slf4j'
    }

    langResources project(path: prj, configuration: 'langResources')
  }

  compileOnly "org.slf4j:slf4j-api:${versions.slf4j}"

  testCompile "org.mockito:mockito-core:${versions.mockito}"
  testCompile "org.apache.logging.log4j:log4j-slf4j-impl:${versions.log4j2}"
}

war {
  from(configurations.langResources, {
    into "resources"
  })
}
