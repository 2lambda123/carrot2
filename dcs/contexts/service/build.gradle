
apply plugin: 'java'
apply plugin: 'war'

configurations {
  langResources
}

dependencies {
  implementation "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"

  implementation(project(':dcs:model'))

  [":core",
   ":lang:polish",
   ":lang:lucene"].each { prj ->
    implementation(project(prj))
    langResources project(path: prj, configuration: 'langResources')
  }

  compileOnly "org.slf4j:slf4j-api:${versions.slf4j}"

  testCompile "org.mockito:mockito-core:${versions.mockito}"
  testCompile "org.apache.logging.log4j:log4j-slf4j18-impl:${versions.log4j2}"
}

// Exclude slf4j from any dependency, it should be part of the container.
configurations.implementation.dependencies.each { module ->
  module.exclude group: 'org.slf4j'
}

war {
  from(configurations.langResources, {
    into "resources"
  })
}
