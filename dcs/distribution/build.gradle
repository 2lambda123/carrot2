import org.apache.tools.ant.filters.*

import java.time.LocalDateTime
import java.time.temporal.ChronoUnit

apply plugin: 'java'

sourceSets {
  integTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integ-test/java')
    }
    resources.srcDir file('src/integ-test/resources')
  }
}

configurations {
  ctxService
  ctxRoot
  ctxFrontend
  ctxProxy

  examples

  integTestImplementation.extendsFrom testImplementation
  integTestRuntimeOnly.extendsFrom testRuntimeOnly

  distZip
  distDir
}

dependencies {
  ctxService project(path: ":dcs:contexts:service", configuration: 'war')
  ctxRoot project(path: ":dcs:contexts:root", configuration: 'war')
  ctxProxy project(path: ":dcs:contexts:proxy", configuration: 'war')
  ctxFrontend project(path: ":dcs:contexts:frontend")

  examples    project(path: ":dcs:examples", configuration: "distribution")

  implementation "com.beust:jcommander:1.72"

  implementation "org.eclipse.jetty:jetty-server:${versions.jetty}"
  implementation "org.eclipse.jetty:jetty-webapp:${versions.jetty}"

  implementation "org.apache.logging.log4j:log4j-core:${versions.log4j2}"
  implementation "org.apache.logging.log4j:log4j-slf4j-impl:${versions.log4j2}"

  integTestImplementation project(":dcs:examples")
}

ext {
  replaceTokens.jarname = 'dcs-launcher'
  outputDir = "$buildDir/dist"
  outputZip = "$buildDir/dist-zip"
}

jar {
  manifest {
    attributes("Main-Class": "org.carrot2.dcs.DcsLauncher")
    attributes("Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '))
  }

  archiveBaseName = replaceTokens.jarname.toString()
}

task collectDependencies(type: Sync) {
  from configurations.runtimeClasspath
  from jar

  into "$outputDir/lib"
}

task collectScripts(type: Sync) {
  from files('src/main/assembly') {
    filteringCharset = 'UTF-8'
    filter(ReplaceTokens, tokens: replaceTokens)
  }
  from legal

  inputs.properties([
    'version': replaceTokens.version,
    'gitRev': gitRev,

    // force refiltering every hour if nothing else changes.
    'tstamp': LocalDateTime.now().truncatedTo(ChronoUnit.HOURS)
  ])

  into "$outputDir"
  preserve {
    include 'lib/'
    include 'web/'
    include 'examples/'
  }
}

task collectCtxService(type: Sync) {
  dependsOn configurations.ctxService
  from zipTree(configurations.ctxService.asPath)
  into "$outputDir/web/service"
  preserve {
    include 'resources/'
  }
}

task collectCtxRoot(type: Sync) {
  dependsOn configurations.ctxRoot
  from zipTree(configurations.ctxRoot.asPath)
  into "$outputDir/web/root"
}

task collectCtxProxy(type: Sync) {
  dependsOn configurations.ctxProxy
  from zipTree(configurations.ctxProxy.asPath)
  into "$outputDir/web/proxy"
}

task collectCtxFrontend(type: Sync) {
  from configurations.ctxFrontend
  into "$outputDir/web/frontend"
}

task collectExamples(type: Sync) {
  from configurations.examples
  into "$outputDir/examples"
}

task distDir {
  dependsOn collectScripts,
            collectDependencies,
            collectCtxService,
            collectCtxRoot,
            collectExamples,
            collectCtxFrontend

  // Until we have a need for it, don't include the proxy context in the distribution.
  // dependsOn collectCtxProxy

  outputs.file("$outputDir")
}

task distZip(type: Zip) {
  archiveFileName = "carrot2-dcs-${project.version}.zip"
  destinationDirectory = file("$outputZip")

  from distDir
  into "${archivesBaseName}/"
}

task runIntegTest(type: Test) {
  dependsOn assemble, ":dcs:contexts:service:test"
  testClassesDirs = sourceSets.integTest.output.classesDirs
  classpath = sourceSets.integTest.runtimeClasspath

  systemProperty "dist.dir", "$outputDir"

  outputs.upToDateWhen { false }
}

task integTest(dependsOn: runIntegTest) {
  group "verification"
  description "Run integration tests"
}

integTest.mustRunAfter test
check.dependsOn integTest
assemble.dependsOn distDir

artifacts {
  distZip tasks.distZip
  distDir file("$outputDir"), {
    builtBy distDir
  }
}

