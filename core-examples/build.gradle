import org.apache.tools.ant.filters.*

apply plugin: 'java-library'

ext {
  replaceTokens = [
    *: versions.collectEntries { k, v -> ["versions." + k, v] },
    'versions.carrot2': project.version
  ]
}

dependencies {
  implementation project(':core')
  testImplementation project(':lang:polish')
  testImplementation project(':lang:lucene')
  testImplementation "org.apache.lucene:lucene-analyzers-icu:${versions.lucene}"
  testImplementation "junit:junit:${versions.junit}"
}

configurations {
  distribution
}

task buildDistribution(type: Sync) {
  inputs.properties(replaceTokens)

  from(project.projectDir, {
    include 'src/**'
    exclude 'src/main/assembly/**'
  })

  from(files('src/main/assembly'), {
    include '**/*'
    filteringCharset = 'UTF-8'
    filter(ReplaceTokens, tokens: replaceTokens, beginToken: '${', endToken: '}')
  })

  into "$buildDir/distribution"
}

artifacts {
  distribution file("$buildDir/distribution"), {
    builtBy tasks.buildDistribution
  }
}
