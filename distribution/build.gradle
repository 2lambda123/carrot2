
plugins {
  id 'base'
}

ext {
  distTarget = file("$buildDir/dist")
  distShadow = file("$buildDir/distCheck")
}

configurations {
  dcs
  examples
  legal
}

dependencies {
  dcs project(path: ":dcs:distribution", configuration: "distDir" )
  examples project(path: ":core-examples", configuration: "distribution" )
}

task collectDependencies(type: Sync) {
  dependsOn ":mavenLocal"
  from legal

  from(configurations.dcs) {
    into "dcs/"
  }

  from(configurations.examples) {
    into "examples/"
  }

  from(rootProject.mavenBuildRepo) {
    into "artifacts/"
  }

  into distTarget
}

task syncDistShadow(type: Sync) {
  from distTarget
  into distShadow
}

task checkExamples(type: GradleBuild) {
  dependsOn assemble, syncDistShadow

  dir = file("${distShadow}/examples")
  tasks = ["test"]
}

task checkDcsExamples(type: GradleBuild) {
  dependsOn assemble, syncDistShadow

  dir = file("${distShadow}/dcs/examples")
  tasks = ["compileJava"]
}

assemble.dependsOn(
  collectDependencies
)

check.dependsOn(checkExamples, checkDcsExamples)
