apply plugin: 'java'

configurations {
  descriptors
}

dependencies {
  implementation project(":core")
  implementation "com.carrotsearch.console:launcher"
}

ext {
  descriptorsDir = file("${buildDir}/descriptors")
}

jar {
  doFirst {
    manifest {
      attributes("Main-Class": "com.carrotsearch.console.launcher.Launcher")
      attributes("Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '))
    }
  }
}

task generateDescriptors(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = 'org.carrot2.infra.docattrs.WriteDescriptorsCommand'
  args = [
    "--algorithms",
    "--recursive",
    "--directory",
    descriptorsDir
  ]

  inputs.files configurations.runtimeClasspath
  outputs.dir descriptorsDir

  doFirst {
    project.delete(descriptorsDir)
    descriptorsDir.mkdirs()
  }
}

artifacts {
  descriptors descriptorsDir, {
    builtBy generateDescriptors
  }
}

assemble.dependsOn generateDescriptors