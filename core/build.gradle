
apply plugin: 'java-library'

configurations {
  jflex
  langResources
}

dependencies {
  api "org.slf4j:slf4j-api"
  api "com.carrotsearch:hppc"

  jflex "de.jflex:jflex"

  langResources fileTree(dir: 'src/main/resources', includes: ['**/*.stopwords.utf8', '**/*.stoplabels.utf8'])
}

sourceSets {
  main {
    java {
      srcDirs = ["src/main/java", "src/main/java-jflex"]
    }
  }
}

task jflex {
  def jflexSource = file('src/main/jflex').toPath()
  def jflexTarget = file('src/main/java-jflex').toPath()

  inputs.files(fileTree(dir: jflexSource, include: "**/*.jflex"))
  outputs.dir(jflexTarget)

  doLast {
    inputs.files.forEach { path ->
      def relative = jflexSource.relativize(path.toPath())

      javaexec {
        classpath {
          configurations.jflex
        }

        main = "jflex.Main"
        args "-nobak", "--noinputstreamctor"
        args "-d", jflexTarget.resolve(relative.getParent())
        args path
      }
    }
  }
}

compileJava.dependsOn jflex

spotless {
  java {
    targetExclude "**/nanojson/*", "**/ExtendedWhitespaceTokenizerImpl.java*"
  }
}

javadoc {
  exclude 'org/carrot2/math/mahout/**'
  exclude '**/nanojson/**'
  exclude '**/ExtendedWhitespaceTokenizerImpl*'
}
