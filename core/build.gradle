
apply plugin: 'java-library'

apply from: rootProject.file("gradle/jflex.gradle")

configurations {
  langResources
  jsondoclet
}

dependencies {
  api "org.slf4j:slf4j-api"
  api "com.carrotsearch:hppc"

  langResources fileTree(dir: 'src/main/resources', includes: ['**/*.stopwords.utf8', '**/*.stoplabels.utf8'])

  jsondoclet 'capital.scalable:spring-auto-restdocs-json-doclet-jdk9:2.0.5'
}

spotless {
  java {
    targetExclude "**/nanojson/*", "**/ExtendedWhitespaceTokenizerImpl.java*"
  }
}

javadoc {
  exclude 'org/carrot2/math/mahout/**'
  exclude '**/nanojson/**'
  exclude '**/ExtendedWhitespaceTokenizerImpl*'
}

task javadocJson(type: Javadoc, dependsOn: compileJava) {
  options.encoding = 'UTF-8'
  source = sourceSets.main.allJava
  classpath = sourceSets.main.compileClasspath
  destinationDir = file("${buildDir}/javadoc-json")

  options.doclet = 'capital.scalable.restdocs.jsondoclet.ExtractDocumentationAsJsonDoclet'
  options.memberLevel = JavadocMemberLevel.PACKAGE

  doFirst {
    options.docletpath = configurations.jsondoclet.toList()
  }
}