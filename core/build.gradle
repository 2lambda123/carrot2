apply plugin: 'java-library'

apply from: rootProject.file("gradle/jflex.gradle")

configurations {
  langResources
  jsonDoclet
  jsonJavadoc
}

dependencies {
  api "org.slf4j:slf4j-api"
  api "com.carrotsearch:hppc"

  langResources fileTree(dir: 'src/main/resources', includes: ['**/*.stopwords.utf8', '**/*.stoplabels.utf8'])

  jsonDoclet project(":infra:jsondoclet")
}

spotless {
  java {
    targetExclude "**/nanojson/*", "**/ExtendedWhitespaceTokenizerImpl.java*"
  }
}

ext {
  jsonJavadocDir = file("${buildDir}/javadoc-json")
}

def javadocExcludes = [
  'org/carrot2/math/mahout/**',
  '**/nanojson/**',
  '**/ExtendedWhitespaceTokenizerImpl*'
]

javadoc {
  exclude javadocExcludes
}

task jsonJavadoc(type: Javadoc, dependsOn: compileJava) {
  dependsOn compileJava
  dependsOn configurations.jsonDoclet

  title = null

  options.encoding = 'UTF-8'
  source = sourceSets.main.allJava
  classpath = sourceSets.main.output + sourceSets.main.compileClasspath

  destinationDir = jsonJavadocDir

  options.doclet = 'com.carrotsearch.jsondoclet.JsonDoclet'
  options.memberLevel = JavadocMemberLevel.PUBLIC

  exclude javadocExcludes

  doFirst {
    options.docletpath = configurations.jsonDoclet.toList()
  }
}

artifacts {
  jsonJavadoc jsonJavadocDir, {
    builtBy jsonJavadoc
  }
}
